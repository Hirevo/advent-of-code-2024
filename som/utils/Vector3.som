Vector3 = (
    | x y z |

    x = ( ^ x )
    y = ( ^ y )
    z = ( ^ z )

    x: newX = ( x := newX )
    y: newY = ( y := newY )
    z: newZ = ( z := newZ )

    = other = (
        ^ (x = other x and: y = other y) and: z = other z.
    )

    + other = (
        ^ self class
            x: x + other x
            y: y + other y
            z: z + other z
    )

    - other = (
        ^ self class
            x: x - other x
            y: y - other y
            z: z - other z
    )

    scalarAdd: scalar = (
        ^ self class
            x: x + scalar
            y: y + scalar
            z: z + scalar
    )

    scalarSub: scalar = (
        ^ self class
            x: x - scalar
            y: y - scalar
            z: z - scalar
    )

    scalarMult: scalar = (
        ^ self class
            x: x * scalar
            y: y * scalar
            z: z * scalar
    )

    scalarDiv: scalar = (
        ^ self class
            x: x / scalar
            y: y / scalar
            z: z / scalar
    )

    length = (
        ^ ((x * x) + (y * y) + (z * z)) sqrt.
    )

    normalize = (
        ^ self scalarDiv: self length.
    )

    hashcode = (
        ^ 524287 * (524287 * x hashcode + y hashcode) + z hashcode.
    )

    clone = (
        ^ self class x: x y: y z: z.
    )

    asString = (
        ^ '(' + x + ', ' + y + ', ' + z + ')'.
    )

    print = (
        self asString print.
    )

    println = (
        self asString println.
    )

    -----------

    x: x y: y z: z = (
        | point |
        point := self new.
        point x: x.
        point y: y.
        point z: z.
        ^ point.
    )
)
